'*******************************************************************
' Имя: CreateShare.vbs
' Язык: VBScript
' Описание: Создание папки для совместного доступа
'*******************************************************************
Option Explicit

' Объявляем переменные
Dim strComputer       ' Имя компьютера
Dim strNamespace      ' Имя пространства имен
Dim objService        ' Объект SWbemService
Dim objShare          ' Объект SWbemObject 
Dim iError            ' Значение, возвращаемое методом

' Объявляем константы
Const FILE_SHARE = 0
Const MAXIMUM_CONNECTIONS = 2
Const ADMIN_SHARE = 2147483648

'********************** Начало *************************************
' Присваиваем начальные значения переменным
strComputer = "."
strNamespace = "Root\CIMV2"

' Подключаемся к пространству имен WMI
Set objService = GetObject("WinMgmts:\\" &_
                                   strComputer & "\" & strNamespace)
' Создаем объкт класса Win32_Share
Set objShare = objService.Get("Win32_Share")

' Создаем папку для совместного доступа
iError = objShare.Create _
    ("C:\Test", "NewShare", FILE_SHARE, MAXIMUM_CONNECTIONS,_
     "Эта папка для совместного доступа создана с помощью WMI.")
' Проверяем ошибки
If iError <> 0 Then 
  ' Выводим сообщение об ошибке
  WScript.Echo "При создании папки для совместного доступа " &_
               " произошла ошибка."
Else
  ' Выводим сообщение об успешном создании общей папки  
  WScript.Echo "Создана папка для совместного доступа."
End If

' Создаем папку для совместного доступа администраторов
iError = objShare.Create _
    ("C:\Test", "Adm_Share$", ADMIN_SHARE,, _
        "Эта папку для совместного доступа администраторов " &_
        " создана с помощью WMI.")
' Проверяем ошибки
If iError <> 0 Then 
  ' Выводим сообщение об ошибке
  WScript.Echo "При создании папки для совместного доступа " &_
               " администраторов  произошла ошибка."
Else
  ' Выводим сообщение об успешном создании общей папки 
  WScript.Echo "Создана папка для совместного доступа администраторов."
End If
'************************* Конец ***********************************
